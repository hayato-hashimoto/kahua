;; -*- coding: euc-jp ; mode: scheme-*-
;; lazy-nqueen.kahua
;; $Id: lazy-nqueen.kahua,v 1.5 2005/07/03 04:02:56 nobsun Exp $
;;
;; This example shows how to lazy style works.
;; 

(use srfi-1)
(use gauche.collection)

;; lazy-queens

(define (lazy-queens board-size)
  (define (queen-cols k)
    (delay
      (let1 ek (whnf k)
	(if (= ek 0)
	    (lazy-cons '() '())
	    (lazy-filter
	     (lambda (positions) (delay (lazy-safe? ek positions)))
	     (lazy-flatmap 
	      (lambda (rest-of-queens) 
		(delay 
		  (lazy-map 
		   (lambda (new-row)
		     (delay 
		       (lazy-adjoin-position new-row ek rest-of-queens)))
		   (lazy-enumerate-interval 1 board-size))))
	      (queen-cols (- ek 1))))))))
  (delay (queen-cols board-size)))

(define (lazy-adjoin-position row k qs)
  (delay (lazy-cons (lazy-cons k row) qs)))

(define (lazy-safe? col positions)
  (delay
    (let ((hd (lazy-car positions))
	  (tl (lazy-cdr positions)))
      (lazy-all (lambda (pos) (lazy-safe-simple? col (lazy-cdr hd) pos)) tl))))

(define (lazy-all p ls)
  (delay
    (let1 els (whnf ls)
      (if (null? els)
	  #t
	  (and (whnf (p (lazy-car ls))) (whnf (lazy-all p (lazy-cdr ls))))))))

(define (lazy-safe-simple? col row pos)
  (delay
    (let ((lu (- (whnf (lazy-cdr pos)) (whnf (lazy-car pos))))
	  (ll (+ (whnf (lazy-cdr pos)) (whnf (lazy-car pos)))))
      (and (not (= lu (- (whnf row) (whnf col))))
	   (not (= ll (+ (whnf row) (whnf col))))
	   (not (= (whnf (lazy-cdr pos)) (whnf row)))))))

(define q6 (lazy-queens 6))
(define q7 (lazy-queens 7))
(define q8 (lazy-queens 8))
(define q9 (lazy-queens 9))
(define q10 (lazy-queens 10))
(define q11 (lazy-queens 11))
(define q12 (lazy-queens 12))

;; page

(define (simple-page-template ttl)
  (lambda (elems)
    `((html (head (title ,ttl))
	    (body (@ (bgcolor "white"))
		  (h1 ,ttl)
		  ,@elems)))))

(define (lister q n s items template)
  (define (show-ans ans)
    (define (show-row r)
      (let1 vec (make-vector q (td: "¡û"))
	(begin
	  (vector-set! vec (- (cdr r) 1) (td: "¡ü"))
	  (vector->list vec))))
    (apply table: (@: (border "1")) 
	   (map (lambda (l) (apply tr: (show-row l))) ans)))
  (lambda ()
    (let* ((curnext (lazy-split-at items n))
	   (cur (lazy-car curnext))
	   (next (lazy-cdr curnext)))
      (template
       (list (apply p:
		    (x->string (+ s 1))
		    "¸ÄÌÜ¤Î²ò"
		    (append (map show-ans (nf cur))
			    (let1 enext (whnf next)
				  (if (null? enext) 
				      '() 
				      (list
				       (a/cont: (@@: (cont 
						      (lister q n (+ s n) 
							      enext template)))
						"Next"))))
			    (list " | "
				  (a/cont:
				   (@@: 
				    (cont (nqueens-start
					   (simple-page-template "N-Queens"))))
				   "Restart")))))))))

(define (nqueens-start template)
  (lambda ()
    (template 
     (list 
      (a/cont: (@@: (cont (lister 6 1 0 q6 
				  (simple-page-template "6-Queens"))))
		    "6-Queens")
      " , "
      (a/cont: (@@: (cont (lister 7 1 0 q7 
				  (simple-page-template "6-Queens"))))
		    "7-Queens")
      " , "
      (a/cont: (@@: (cont (lister 8 1 0 q8
 				  (simple-page-template "8-Queens"))))
 	       "8-Queens")
      " , "
      (a/cont: (@@: (cont (lister 9 1 0 q9
				  (simple-page-template "9-Queens"))))
 	       "9-Queens")
      " , "
      (a/cont: (@@: (cont (lister 10 1 0 q10
				  (simple-page-template "10-Queens"))))
	       "10-Queens")
      " , "
      (a/cont: (@@: (cont (lister 11 1 0 q11
				  (simple-page-template "11-Queens"))))
	       "11-Queens")
      " , "
      (a/cont: (@@: (cont (lister 12 1 0 q12
				  (simple-page-template "12-Queens"))))
	       "12-Queens")
      ))))

(initialize-main-proc (nqueens-start (simple-page-template "N-Queens")))

;; Lazy staffs

(define (whnf obj)
  (let1 forced (force obj)
    (if (promise? forced)
	(whnf forced)
	forced)))

(define (nf obj)
  (let1 eobj (whnf obj)
    (if (pair? eobj) 
	(cons (nf (car eobj)) (nf (cdr eobj)))
	eobj)))

(define (lazy-cons hd tl)
  (delay (cons hd tl)))

(define (lazy-car obj)
  (delay (car (whnf obj))))

(define (lazy-cdr obj)
  (delay (cdr (whnf obj))))


(define (lazy-map f obj)
  (delay 
    (let1 eobj (whnf obj)
      (if (null? eobj)
	  '()
	  (lazy-cons (f (lazy-car eobj)) 
		     (lazy-map f (lazy-cdr eobj)))))))

(define (lazy-filter p obj)
  (delay 
    (let1 eobj (whnf obj)
      (if (null? eobj)
	  '()
	  (let ((hd (lazy-car eobj))
		(tl (lazy-cdr eobj)))
	    (if (whnf (p hd))
		(lazy-cons hd (lazy-filter p tl))
		(lazy-filter p tl)))))))

(define (lazy-take obj n)
  (delay
    (let1 en (whnf n)
      (if (<= en 0)
	  '()
	  (let1 eobj (whnf obj)
	    (if (null? eobj)
		'()
		(lazy-cons (lazy-car eobj) 
			   (lazy-take (lazy-cdr eobj) (- en 1)))))))))

(define (lazy-split-at obj n)
  (delay
    (let1 eobj (whnf obj)
      (if (null? eobj)
	  (lazy-cons '() '())
	  (let1 en (whnf n)
	    (if (= en 0)
		(lazy-cons '() eobj)
		(let1 rc (lazy-split-at (lazy-cdr eobj) (- en 1))
                  (lazy-cons (lazy-cons (lazy-car eobj) (lazy-car rc))
			     (lazy-cdr rc)))))))))
			     
(define (lazy-append xs ys)
  (delay
    (let1 exs (whnf xs)
      (if (null? exs)
          ys
	  (lazy-cons (lazy-car exs) (lazy-append (lazy-cdr exs) ys))))))

(define (lazy-foldr f i xs)
  (delay
    (let1 exs (whnf xs)
      (if (null? exs)
	  i
          (f (lazy-car exs) (lazy-foldr f i (lazy-cdr exs)))))))

(define (lazy-enumerate-interval low high)
  (delay
    (let ((elow (whnf low))
	  (ehigh (whnf high)))
      (if (> elow ehigh)
          '()
          (lazy-cons elow (lazy-enumerate-interval (+ elow 1) ehigh))))))

(define (lazy-flatmap proc seq)
  (delay
    (lazy-foldr lazy-append '() (lazy-map proc seq))))

